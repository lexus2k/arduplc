# Описание входов:
# _1 - Верхний датчик
# _2 - Средний датчик
# _3 - Нижний датчик
# _4 - Датчик платформы под прессом
# _5 - Датчик платформы в убранном состоянии
#
# Описание выходов:
# 1 - Движение пресса вверх
# 2 - Движение пресса вниз
# 3 - Движение платформы под пресс
# 4 - Убирание платформы
# 

# Это состояние всегда должно быть первым. С него начинается программа
STATE INIT
# Выключение всех выходов
SET 1 OFF
SET 2 OFF
SET 3 OFF
SET 4 OFF
EVENTS:
# Если пресс уже вверху, а платформа убрана переходим в готовность
ON _1&_5 READY
# Если платформа не убрана, идем убирать платформу
ON _1    PRESS_READY
# Пресс не поднят, идем поднимать пресс
GOTO INIT_PRESS


STATE INIT_PRESS
# Поднимаем пресс
SET 1 ON
EVENTS:
# Если пресс уже вверху, а платформа убрана переходим в готовность
ON _1&_5 READY
# Если пресс вверху, но платформа не убрана, убираем ее
ON _1    PRESS_READY
EXIT:
# При выходе из этого состояния выключаем движение пресса вверх
SET 1 OFF

STATE PRESS_READY
# Убираем платформу
SET 4 ON
EVENTS:
# Если убрана, то переходим в готовность
ON _5 READY
EXIT:
# Останавливаем движение платформы
SET 4 OFF

# Мы готовы начать работать, опускаем пресс до середины
STATE READY
SET 2 ON
EVENTS:
ON _2 LEFT_1
EXIT:
SET 2 OFF

# Теперь двигаем платформу под пресс
STATE LEFT_1
SET 3 ON
EVENTS:
ON _4 RIGHT_1
EXIT:
SET 3 OFF

# Убираем платформу из-под пресса
STATE RIGHT_1
PAUSE 1000
SET 4 ON
EVENTS:
ON _5 LEFT_2
EXIT:
SET 4 OFF

# И снова повторяем: двигаем платформу под пресс
STATE LEFT_2
SET 3 ON
EVENTS:
ON _4 RIGHT_2
EXIT:
SET 3 OFF

# Убираем платформу из-под пресса
STATE RIGHT_2
PAUSE 1000
SET 4 ON
EVENTS:
ON _5 DOWN
EXIT:
SET 4 OFF

# Опускаем пресс до конца вниз
STATE DOWN
SET 2 ON
EVENTS:
ON _3 UP
EXIT:
SET 2 OFF

# Ждем перед поднятием секунду и поднимаем пресс до самой верхней точки и начинаем цикл сначала
STATE UP
PAUSE 1000
SET 1 ON
EVENTS:
ON _1 READY
EXIT:
SET 1 OFF
